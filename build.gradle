buildscript {
    repositories {
        mavenCentral()
    }
}

ext {
    apacheCommonsLang3 = "3.14.0"
    googleJsr305Version = "3.0.2"
    jacksonVersion = "2.16.1"
    junitVersion = "5.10.1"
    jacocoVersion = "0.8.8"
    jacksonDatabindNullableVersion = "0.2.6"
    mockitoVersion = "3.2.4"
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'java'
    apply plugin: 'jacoco'

    group = 'com.thousandeyes.api'
    version = '1.0.0-SNAPSHOT'

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    // Some text from the schema is copy pasted into the source files as UTF-8
    // but the default still seems to be to use platform encoding
    tasks.withType(JavaCompile) {
        configure(options) {
            options.encoding = 'UTF-8'
        }
    }
    javadoc {
        options.encoding = 'UTF-8'
    }

    task execute(type: JavaExec) {
        classpath = sourceSets.main.runtimeClasspath
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    def jacocoXmlReport = "${project.rootDir}/${project.name}/reports/jacoco/${project.name}.xml"

    jacoco {
        toolVersion = "$jacocoVersion"
    }

    jacocoTestReport {
        sourceSets sourceSets.main
        executionData.from = fileTree(project.buildDir).include("/jacoco/*.exec")
        reports {
            csv.required = false
            html.required = false
            xml.required = true
            xml.outputLocation.set(file(jacocoXmlReport))
        }
    }

    test {
        useJUnitPlatform()
        ignoreFailures true
        finalizedBy jacocoTestReport
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    }
}
