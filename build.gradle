import com.vanniktech.maven.publish.SonatypeHost

buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "com.vanniktech.maven.publish" version "0.29.0"
}

ext {
    apacheCommonsLang3 = "3.14.0"
    googleJsr305Version = "3.0.2"
    mockitoVersion = "5.3.0"
    jacksonDatabindNullableVersion = "0.2.6"
    jacksonVersion = "2.16.1"
    jacocoVersion = "0.8.8"
    junitVersion = "5.10.1"
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'com.vanniktech.maven.publish'
    group = 'com.thousandeyes.sdk'
}

subprojects {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    // Some text from the schema is copy pasted into the source files as UTF-8
    // but the default still seems to be to use platform encoding
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    javadoc {
        options.encoding = 'UTF-8'
    }

    task execute(type: JavaExec) {
        classpath = sourceSets.main.runtimeClasspath
    }

    jacocoTestReport {
        dependsOn test
        reports {
            csv.required = false
            html.required = false
            xml.required = true
        }
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    }

    mavenPublishing {
        coordinates("com.thousandeyes.sdk", project.name, project.version)
        pom {
            name = "ThousandEyes Java SDK - ${project.name}"
            description = 'A set of Java client libraries for the Thousandeyes v7 API. For more information, visit: https://developer.cisco.com/docs/thousandeyes/v7/'
            url = 'https://github.com/thousandeyes/thousandeyes-sdk-java'
            licenses {
                license {
                    name = 'The Apache License, Version 2.0'
                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    name = 'ThousandEyes API Team'
                    email = 'api-team@thousandeyes.com'
                    organization = 'Cisco - ThousandEyes'
                    organizationUrl = 'https://www.thousandeyes.com'
                }
            }
            scm {
                connection = 'scm:git:git://github.com/thousandeyes/thousandeyes-sdk-java.git'
                developerConnection = 'scm:git:ssh://github.com/thousandeyes/thousandeyes-sdk-java.git'
                url = 'https://github.com/thousandeyes/thousandeyes-sdk-java/tree/main'
            }
        }
        publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL, true)

        signAllPublications()
    }
}
